// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.70'
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-rc01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

ext.versions = [
        "compileSdk"      : 29,
        "minSdk"          : 21,
        "targetSdk"       : 29,
        "archTesting"     : "2.1.0",
        "constraintlayout": "1.1.3",
        "coroutines"      : "1.3.6",
        "dagger"          : "2.26",
        "fragment"        : "1.2.4",
        "lifecycle"       : "2.2.0",
        "moshi"           : "1.9.2",
        "recyclerview"    : "1.1.0",
        "retrofit"        : "2.8.1",
        "truth"           : "1.0.1"
]

ext.deps = [
        "androidx"  : [
                "archtesting"     : "androidx.arch.core:core-testing:${versions.archTesting}",
                "constraintlayout": "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                "fragment"        : "androidx.fragment:fragment-ktx:${versions.fragment}",
                "fragmenttesting" : "androidx.fragment:fragment-testing:${versions.fragment}",
                "livedata"        : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}",
                "recyclerview"    : "androidx.recyclerview:recyclerview:${versions.recyclerview}",
                "viewmodel"       : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"
        ],
        "coroutines": [
                "android": "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
                "test"   : "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
        ],
        "dagger"    : [
                "core"    : "com.google.dagger:dagger:${versions.dagger}",
                "compiler": "com.google.dagger:dagger-compiler:${versions.dagger}"
        ],
        "moshi"     : [
                "core"  : "com.squareup.moshi:moshi:${versions.moshi}",
                "kotlin": "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"
        ],
        "retrofit"  : [
                "core" : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                "moshi": "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
        ],
        "truth"     : "com.google.truth:truth:${versions.truth}"
]

subprojects {
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
